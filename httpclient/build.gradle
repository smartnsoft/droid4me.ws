apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

android
{
  compileSdkVersion 25
  buildToolsVersion "25.0.3"

  useLibrary "org.apache.http.legacy"

  defaultConfig
  {
    minSdkVersion 14
    targetSdkVersion 25

    versionCode 3
    versionName "1.1.0"
  }

  buildTypes
  {
    release
    {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

dependencies
{
  //Smart&Soft
  compile files('libs/droid4me-2.4.0.jar')
  compile files('libs/droid4me.ext-2.4.0.jar')

  //Third party libraries
  compile 'org.apache.httpcomponents:httpcore:4.4'
  compile 'org.apache.httpcomponents:httpmime:4.5.2'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1'
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  title = "httpclient API"
  description "Javadoc for the httpclient web service caller implementation"
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = ["http://docs.oracle.com/javase/7/docs/api/", "http://d.android.com/reference/"]

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId "com.smartnsoft"
      artifactId "httpclientwebservicecaller"
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", "extension of the droid4me framework which the httpclient contains web service caller implementation")
        root.appendNode("name", "httpclientwebservicecaller")
        root.appendNode("url", "hhttps://github.com/smartnsoft/droid4me.ws")
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  user = bintrayUsername
  key = bintrayKey

  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = "httpclientwebservicecaller"
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/smartnsoft/droid4me.ws.git"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = "extension of the droid4me framework which contains the httpclient web service caller implementation"
      released  = new Date()
      vcsTag = "httpclientwebservicecaller-" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/httpclientwebservicecaller/;publish=1")
      {
        authentication(userName: bintrayUsername, password: bintrayKey)
      }

      snapshotRepository(url: nexusSnapshotUrl)
      {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      pom.project
      {
        name "httpclientwebservicecaller"
        groupId "com.smartnsoft"
        artifactId "httpclientwebservicecaller"
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}